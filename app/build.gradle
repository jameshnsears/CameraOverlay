plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.gms.google-services'
    id "io.gitlab.arturbosch.detekt" version "1.19.0"
    id "org.jlleitschuh.gradle.ktlint" version "10.2.1"
    id "org.sonarqube" version "3.3"
}

apply from: '../detekt.gradle'
apply from: '../ktlint.gradle'
apply from: '../jacoco.gradle'
apply from: '../sonarqube.gradle'

def localPropertiesFile = rootProject.file("local.properties")
def localProperties = new Properties()

if (!localPropertiesFile.exists()) {
    localProperties.setProperty("RELEASE_STORE_PASSWORD", "")
    localProperties.setProperty("RELEASE_KEY_PASSWORD", "")
    localProperties.setProperty("RELEASE_KEY_ALIAS", "")
    localProperties.setProperty("RELEASE_STORE_FILE", "keystore.jks")
    Writer writer = new FileWriter(localPropertiesFile, false)
    localProperties.store(writer, "empty, as creating the file is done manually via gpg")
    writer.close()

    file(project(':app').projectDir.path + "/keystore.jks").text = ""
}

localProperties.load(new FileInputStream(localPropertiesFile))

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.github.jameshnsears.cameraoverlay"
        minSdk 29
        targetSdk 31
        versionCode 12931
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    testOptions {
        animationsDisabled true

        unitTests {
            includeAndroidResources = true

            all {
                useJUnitPlatform()
                maxHeapSize = "1024m"
                jacoco {
                    includeNoLocationClasses = true
                    excludes = ['jdk.internal.*']
                }
            }

            returnDefaultValues = true
        }
    }

    buildTypes {
        def gitHash = { ->
            def stdout = new ByteArrayOutputStream()
            exec {
                commandLine 'git', 'rev-parse', '--short=8', 'HEAD'
                standardOutput = stdout
            }
            return stdout.toString().trim()
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField("String", "GIT_HASH", "\"$gitHash\"")

            ndk {
                debugSymbolLevel = 'full'
            }
        }

        debug {
            testCoverageEnabled true
            buildConfigField("String", "GIT_HASH", "\"$gitHash\"")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }

    signingConfigs {
        googleplay {
            keyAlias localProperties['RELEASE_KEY_ALIAS']
            keyPassword localProperties['RELEASE_KEY_PASSWORD']
            storeFile file(localProperties['RELEASE_STORE_FILE'])
            storePassword localProperties['RELEASE_STORE_PASSWORD']
        }
    }

    flavorDimensions 'Version'
    productFlavors {
        'googleplay' {
            dimension 'Version'
            versionNameSuffix "-googleplay"
            signingConfig signingConfigs.googleplay
        }
        'fdroid' {
            dimension 'Version'
            versionNameSuffix "-fdroid"
            isDefault true
        }
    }

    sourceSets {
        androidTest {
            resources.srcDirs += ['src/androidTest/ressources', 'src/androidTest/res']
        }
        fdroid {
            assets.srcDirs = ['src/main/assets']
            java.srcDirs = ['src/main/java', 'src/fdroid/java']
            resources.srcDirs += ['src/test/resources']
        }
        googleplay {
            assets.srcDirs = ['src/main/assets']
            java.srcDirs = ['src/main/java']
            resources.srcDirs += ['src/test/resources']
        }
    }
    lint {
        abortOnError false
        baseline file("$rootDir/config/lint/baseline.xml")
        checkAllWarnings true
        htmlReport true
        warningsAsErrors true
        xmlReport true
    }
}

dependencies {
    // https://maven.google.com/web/index.html

    // https://developer.android.com/jetpack/androidx/versions

    // junit 4
    androidTestImplementation 'androidx.activity:activity-compose:1.4.0'
    androidTestImplementation "androidx.compose.foundation:foundation:$compose_version"
    androidTestImplementation "androidx.compose.material:material:$compose_version"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    androidTestImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.ext:truth:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    androidTestImplementation 'com.squareup.okhttp3:mockwebserver:4.9.3'
    androidTestImplementation 'io.mockk:mockk-android:1.12.3'

    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.8.1'

    implementation 'androidx.activity:activity-compose:1.4.0'
    implementation 'androidx.activity:activity-ktx:1.4.0'
    implementation "androidx.compose.foundation:foundation:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"
    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation 'androidx.datastore:datastore-preferences:1.0.0'
    implementation "androidx.exifinterface:exifinterface:1.3.3"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.1'
    implementation 'androidx.navigation:navigation-compose:2.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.jakewharton.timber:timber:5.0.1'

    implementation 'io.coil-kt:coil-compose:2.0.0-rc01'

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.0'
    implementation platform('com.google.firebase:firebase-bom:28.4.2')
    implementation project(path: ':module:common')
    implementation project(path: ':module:permissions')
    implementation project(path: ':module:opencv')

    // junit 5
    testImplementation 'com.google.truth:truth:1.1.3'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.2'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-debug:1.6.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}
