plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id "io.gitlab.arturbosch.detekt" version "1.21.0"
    id "org.jlleitschuh.gradle.ktlint" version "11.0.0"
}

apply from: '../../detekt.gradle'
apply from: '../../ktlint.gradle'

android {
    namespace 'com.github.jameshnsears.cameraoverlay.permissions'
    compileSdk 33

    defaultConfig {
        minSdk 24
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        consumerProguardFiles "consumer-rules.pro"
    }

    testOptions {
        animationsDisabled true
    }

    packagingOptions {
        resources {
            excludes += [
                    '**/module-info.class',
                    'LICENSE',
                    'README.md',
                    'META-INF/LICENSE.md',
                    'META-INF/LICENSE-notice.md'
            ]
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            testCoverageEnabled true
            buildConfigField("boolean", "GITHUB_ACTION", gitHubAction())
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion "1.3.2"
    }

    flavorDimensions 'Version'
    productFlavors {
        'googleplay' {
            dimension 'Version'
        }
        'fdroid' {
            dimension 'Version'
            isDefault true
        }
    }

    sourceSets {
        fdroid {
            java.srcDirs = ['src/main/java']
        }
        googleplay {
            java.srcDirs = ['src/main/java']
        }
    }
}

dependencies {
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4:1.3.3'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.10'

    implementation 'androidx.activity:activity-compose:1.6.1'
    implementation 'androidx.compose.foundation:foundation:1.3.1'
    implementation "androidx.compose.material3:material3:1.0.1"
    implementation 'androidx.compose.material:material-icons-extended:1.3.1'
    implementation 'androidx.compose.runtime:runtime-livedata:1.3.3'
    implementation 'androidx.compose.ui:ui:1.3.3'
    implementation 'androidx.compose.ui:ui-tooling:1.3.3'
    implementation 'androidx.datastore:datastore-preferences:1.0.0'
    implementation 'com.jakewharton.timber:timber:5.0.1'
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(path: ':module:common')
}

def gitHubAction() {
    /*
     when running in a GitHub Action...
     ./gradlew --no-configuration-cache -PgitHubAction :app:connectedFdroidDebugAndroidTest
     */

    def value = project.getProperties().get("gitHubAction") != null ? "true": "false"
    println 'gitHubAction=' + value
    return value
}
