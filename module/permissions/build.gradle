plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id "io.gitlab.arturbosch.detekt" version "1.18.1"
    id "org.jlleitschuh.gradle.ktlint" version "11.0.0"
}

apply from: '../../detekt.gradle'
apply from: '../../ktlint.gradle'


android {
    compileSdk 33

    defaultConfig {
        minSdk 29
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        // The following argument makes the Android Test Orchestrator run its
        // "pm clear" command after each test invocation. This command ensures
        // that the app's state is completely cleared between tests.

        // 'true' means no jacoco coverage + no permissions granted, except for SYSTEM_ALERT_WINDOW
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    testOptions {
        animationsDisabled true

        // no manifest permissions granted automatically if not using orchestrator
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            testCoverageEnabled true
            buildConfigField("boolean", "GITHUB_ACTION", gitHubAction())
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion "1.3.0"
    }


    flavorDimensions 'Version'
    productFlavors {
        'googleplay' {
            dimension 'Version'
        }
        'fdroid' {
            dimension 'Version'
            isDefault true
        }
    }

    sourceSets {
        fdroid {
            java.srcDirs = ['src/main/java', 'src/fdroid/java']
        }
        googleplay {
            java.srcDirs = ['src/main/java']
        }
    }
    lint {
        abortOnError false
        baseline file("$rootDir/config/lint/baseline.xml")
        checkAllWarnings true
        htmlReport true
        warningsAsErrors true
        xmlReport true
    }
}

dependencies {
    // junit 4
    androidTestImplementation 'androidx.activity:activity-compose:1.5.1'
    androidTestImplementation "androidx.compose.foundation:foundation:1.2.1"
    androidTestImplementation "androidx.compose.material:material:1.2.1"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:1.2.1"
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.ext:truth:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    androidTestImplementation 'io.mockk:mockk-android:1.12.7'

    debugImplementation "androidx.compose.ui:ui-test-manifest:1.2.1"
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.9.1'

    implementation 'androidx.activity:activity-compose:1.5.1'
    implementation 'androidx.activity:activity-ktx:1.5.1'
    implementation "androidx.compose.foundation:foundation:1.2.1"
    implementation "androidx.compose.material:material:1.2.1"
    implementation "androidx.compose.material:material-icons-extended:1.2.1"
    implementation "androidx.compose.runtime:runtime-livedata:1.2.1"
    implementation "androidx.compose.ui:ui:1.2.1"
    implementation "androidx.compose.ui:ui-tooling:1.2.1"
    implementation 'androidx.datastore:datastore-preferences:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.1'
    implementation 'androidx.navigation:navigation-compose:2.5.1'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'com.jakewharton.timber:timber:5.0.1'
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'
    implementation project(path: ':module:common')
}

def gitHubAction() {
    /*
     when running in a GitHub Action...
     ./gradlew --no-configuration-cache -PgitHubAction :app:connectedFdroidDebugAndroidTest
     */

    def value = project.getProperties().get("gitHubAction") != null ? "true": "false"
    println 'gitHubAction=' + value
    return value
}
